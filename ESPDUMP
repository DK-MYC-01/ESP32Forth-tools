(  ESPDUMP.TXT  )
\ Goal: Allways show all requested bytes (and may be some more), at least in 1 full line!
\ OBS: Dump line will always start on an address  with low order nibble = 0 e.g.:  FF4CAAA0 
\         Dumping an "illegal address" will chrash Forth and your ESP32 wil restart! (like original DUMP)

\  12345678 00 11 22 33 44 55 66 77 - 00 1122 33 44 55 66 77  AAAAAAAA BBBBBBBB 
\  Offset       ----------8Bytes---------    ----------8Bytes---------  -------USASCII--------

base @	\ remember current base
hex	\ all dumping must show HEX bytes

variable myOffset
variable myLineCount
   	 
: MyOffset. 	myOffset @ <# # # # # # # # #s #> TYPE space  ;    \ Print a 32bit ADR, 8 CHARS WIDE
: Byte. 		<# # #s space #> type ;			           \ Print a byte (2 nibbles) and a space
: b>c. 		dup 21 < if drop 7E then dup 7E > if drop 7E then emit ;  \ Print a valid USASCII char or a "~" (7E)
\		 "." not used as normal seen in dumps, as a "." is often used in FORTH code
: 8Bytes. 		8 0 
		do myOffset @ 
		    dup  
		    c@ Byte.
		   1+ myOffset !
		loop 
;

: myDumpPrep ( adr n ---)  	over ( --- adr n adr )
			000F and ( --- adr n xtra )
			+  	( --- adr #BytesToPrint? )
			1- 0 max ( --- adr #BytesToPrint )
			10 /	( --- adr #LinesToPrint? )
			1+
			myLineCount !  ( --- adr )
			FFFFFFF0 and  myOffset !  ;  \ dump min. 16 bytes aligned  nnn0

: 0xDUMP     ( adr n --- ) 	\ "Zero Hex DUMP"
	base @		( --- adr n base)    \ remember current base
	-rot		( --- base adr n )
	hex		\ DUMP must show HEX bytes
	myDumpPrep  	( --- base  )
	begin	cr MyOffset.  8Bytes.  ."  -"   8Bytes. 
		\ hex bytes printed, now show valid USACII code bytes, or an "." :
		myOffset @ 10 - 
		myOffset !  \ go 16 bytes back again, and now print USASCII:
		space
		10 0	do myOffset @	\ print 16 USASCII chars
			    dup 
			    1+ myOffset !
			    c@ b>c.			    
			    I 7 = if space then \ insert a blank seperator after 8 USASCII chars
			loop
		myLineCount @ 1- 
		dup myLineCount ! ( all lines printed? )
		0=		( --- flag )
	until
	cr	( --- base  )
	base !	( --- ) 		\ reset to org. base
;

base !	\ back to  former base
